buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
		mavenCentral()
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
  id "jacoco"
  id "org.sonarqube" version "2.6"
}

sonarqube {
    properties {
        property "sonar.projectKey", "annotated"
        property "sonar.projectName", "Annotated"
    }
}

allprojects {
	apply plugin: 'net.minecraftforge.gradle.forge'
	apply plugin: 'jacoco'

	jacoco {
		toolVersion = "0.8.3"
	}

	ext {
		mcVersion = "1.11.2"
		libVersion = "$mcVersion-1.0"
		processorVersion = "$libVersion-1.0"
	}
	
	version = processorVersion
	group = "fr.max2.annotated"
	archivesBaseName = "Annotated"
	
	sourceCompatibility = targetCompatibility = "1.8"
	compileJava {
		sourceCompatibility = targetCompatibility = "1.8"
	}
	
	minecraft {
		version = "1.11.2-13.20.0.2228"
		runDir = "run"
		
		mappings = "snapshot_20170610"
	}
	
	
	processResources {
		inputs.property "version", project.version
		inputs.property "mcversion", project.minecraft.version
		
		from(sourceSets.main.resources.srcDirs) {
			include 'mcmod.info'
			
			expand 'version':project.version, 'mcversion':project.minecraft.version
		}
		
		from(sourceSets.main.resources.srcDirs) {
			exclude 'mcmod.info'
		}
	}
	
	repositories {
		mavenCentral()
	}

	dependencies {
		testCompile 'junit:junit:4.12'
	}
}

task deobfJar(type: Jar) {
	from project(':lib').sourceSets.main.output
	from project(':processor').sourceSets.main.output

    classifier = 'dev-deobf'

}

jar {
	from project(':lib').sourceSets.main.output
	from project(':processor').sourceSets.main.output
	
	classifier = 'dev'
}

sourceJar {
	includeEmptyDirs = false
	from (project(':lib').sourceSets.main.allSource) {
		include '**/api/**'
	}
	from (project(':processor').sourceSets.main.allSource) {
		include '**/api/**'
	}
}

tasks.assemble.dependsOn('deobfJar')

artifacts {
    archives deobfJar
}