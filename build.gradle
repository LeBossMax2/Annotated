buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
		mavenCentral()
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
	}
}

plugins {
  id "org.sonarqube" version "2.6"
}

allprojects {
	apply plugin: 'net.minecraftforge.gradle.forge'
	apply plugin: 'org.junit.platform.gradle.plugin'
	
	
	version = "1.0.0"
	group = "fr.max2.packeta"
	archivesBaseName = "Packeta"
	
	sourceCompatibility = targetCompatibility = "1.8"
	compileJava {
		sourceCompatibility = targetCompatibility = "1.8"
	}
	
	minecraft {
		version = "1.11.2-13.20.0.2228"
		runDir = "run"
		
		mappings = "snapshot_20170610"
	}
	
	
	processResources {
		inputs.property "version", project.version
		inputs.property "mcversion", project.minecraft.version
		
		from(sourceSets.main.resources.srcDirs) {
			include 'mcmod.info'
			
			expand 'version':project.version, 'mcversion':project.minecraft.version
		}
		
		from(sourceSets.main.resources.srcDirs) {
			exclude 'mcmod.info'
		}
	}
	
	repositories {
		mavenCentral()
	}
	
	dependencies {
		testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
		testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
	}
}

task deobfJar(type: Jar) {
	from project(':api').sourceSets.main.output
	from project(':processor').sourceSets.main.output

    classifier = 'dev-deobf'

}

jar {
	from project(':api').sourceSets.main.output
	from project(':processor').sourceSets.main.output
	
	classifier = 'dev'
}

tasks.assemble.dependsOn('deobfJar')